import dependencies.Libs
import dependencies.Versions

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        consumerProguardFiles 'consumer-rules.pro'
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation Libs.Kotlin.stdlib
    implementation Libs.AndroidX.appcompat
}

afterEvaluate {

    task dokkaJar(type: Jar) {
        group = JavaBasePlugin.DOCUMENTATION_GROUP
        description = "Assembles Kotlin docs with Dokka"
        archiveClassifier.set('sources')
        from tasks.dokkaHtml
        dependsOn(tasks.dokkaHtml)
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.getByName('main').java.srcDirs
    }
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                group = 'com.github.fornewid'
                artifactId = 'neumorphism'
                version = '0.3.1'

                pom {
                    name = 'Neumorphism'
                    description = 'Try to Neumorphism designs in Android.'
                    url = 'https://github.com/fornewid/neumorphism'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'fornewid'
                            name = 'SOUP'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com/fornewid/neumorphism.git'
                        developerConnection = 'scm:git@github.com/fornewid/neumorphism.git'
                        url = 'https://github.com/fornewid/neumorphism'
                    }
                }
            }
        }
    }
}
